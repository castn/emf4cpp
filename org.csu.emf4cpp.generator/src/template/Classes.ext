/*
 * Classes.ext
 * Copyright (C) CÃ¡tedra SAES-UMU 2010 <andres.senac@um.es>
 * Copyright (C) INCHRON GmbH 2016 <soeren.henning@inchron.com>
 * 
 * EMF4CPP is free software: you can redistribute it and/or modify it
 * under the terms of the GNU Lesser General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * EMF4CPP is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
 * See the GNU Lesser General Public License for more details.
 * 
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
 
import org::eclipse::emf::ecore;

extension template::FQN;
extension template::Attributes;

List[String] getSuperTypeIncludeFiles(EClass this) :  
	this.ESuperTypes.getFQN("/")
;

List[String] getEnumIncludeFiles(EClass this) :
	this.EAllAttributes.select(e|EEnum.isInstance(e.EType)).EType.getFQN("/")
;

List[String] getReferencesIncludeFiles(EClass this) :  
	this.EAllReferences.select(e|EClass.isInstance(e.EType)).EType.getFQN("/")
;

List[String] getAttributesIncludeFiles(EClass this) :  
	this.EAllAttributes.select(e|EClass.isInstance(e.EType)).EType.getFQN("/")
;

List[String] getOperationsIncludeFiles(EClass this) :  
	this.EAllOperations.select(e|EClass.isInstance(e.EType)).EType.getFQN("/")
;

List[String] getArgumentsIncludeFiles(EClass this) :
	this.EAllOperations.EParameters.EType.select(e|EClass.isInstance(e)).getFQN("/")
;

int getFeatureIDValue(EClass this, EStructuralFeature ef) :
	this.EAllStructuralFeatures.indexOf(ef)
;

int getFeatureCount(EClass this) :
	this.EAllStructuralFeatures.size
;

Set[String] getAllIncludeFiles(EClass this) : 
	getSuperTypeIncludeFiles().addAll(
		getReferencesIncludeFiles()		
	).addAll(
		getAttributesIncludeFiles()
	).addAll(
		getOperationsIncludeFiles()
	).addAll(
		getArgumentsIncludeFiles()
	).toSet()
;

List[String] getHPPIncludeFiles(EClass this) : 
	getSuperTypeIncludeFiles().addAll(getEnumIncludeFiles())
;

Set[EPackage] getNeededPackages(EClass this) :
	this.EAllStructuralFeatures.EType.select(e|e.EPackage != this.EPackage).EPackage.toSet()
;